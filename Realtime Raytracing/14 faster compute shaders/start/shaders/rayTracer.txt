#version 430

struct Sphere {
    vec3 center;
    float radius;
    vec3 color;
    float roughness;
};

struct Node {
    vec3 min_corner;
    int sphere_count;
    vec3 max_corner;
    int contents;
};

struct Camera {
    vec3 position;
    vec3 forwards;
    vec3 right;
    vec3 up;
};

struct Ray {
    vec3 origin;
    vec3 direction;
};

struct RenderState {
    vec3 color;
    float t;
    vec3 position;
    bool hit;
    vec3 normal;
    float roughness;
};

// input/output
layout(local_size_x = 8, local_size_y = 8) in;
layout(rgba32f, binding = 0) uniform image2D img_output;

//Scene data
uniform Camera viewer;
layout(rgba32f, binding = 1) readonly uniform image2D objects;
layout(rgba32f, binding = 2) readonly uniform image2D nodes;
layout(rgba32f, binding = 3) readonly uniform image2D sphere_lookup_table;
uniform samplerCube sky_cube;
uniform float sphereCount;
uniform float nodeCount;

RenderState trace(Ray ray);

Sphere unpackSphere(int index);

Node unpackNode(int index);

int lookupSphereIndex(int index);

RenderState hit(Ray ray, Sphere sphere, float tMin, float tMax, RenderState renderstate);

float hit(Ray ray, Node node, float nearestHit);

void main() {

    ivec2 pixel_coords = ivec2(gl_GlobalInvocationID.xy);
    ivec2 screen_size = imageSize(img_output);

    vec3 finalColor = vec3(0.0);
    
    float horizontalCoefficient = (float(pixel_coords.x) * 2 - screen_size.x) / screen_size.x;
    
    float verticalCoefficient = (float(pixel_coords.y) * 2 - screen_size.y) / screen_size.x;

    Ray ray;
    ray.origin = viewer.position;
    ray.direction = viewer.forwards + horizontalCoefficient * viewer.right + verticalCoefficient * viewer.up;
    
    vec3 pixel = vec3(1.0);
    RenderState renderState;

    for (int bounce = 0; bounce < 4; bounce++) {
        

        renderState = trace(ray);

        //unpack color
        pixel = pixel * renderState.color;

        //early exit
        if (!renderState.hit) {
            break;
        }

        //set up ray for next trace
        ray.origin = renderState.position;
        ray.direction = normalize(reflect(ray.direction, renderState.normal));

    }

    if (renderState.hit) {
        pixel = vec3(0.0);
    }

    finalColor = pixel;

    imageStore(img_output, pixel_coords, vec4(finalColor,1.0));
}

RenderState trace(Ray ray) {

    RenderState renderState;
    renderState.hit = false;
    vec3 unit_direction = normalize(ray.direction);
    float nearestHit = 9999999;

    Node node = unpackNode(0);
    Node stack[15];
    int stackPos = 0;

    while (true) {
    
        if (node.sphere_count > 0) {

            int a = node.contents;
            int b = node.sphere_count;
    
            for (int i = a; i < a + b; i++) {

                int sphereIndex = lookupSphereIndex(i);

                RenderState newRenderState = hit(ray, unpackSphere(sphereIndex), 0.001, nearestHit, renderState);

                if (newRenderState.hit) {
                    nearestHit = newRenderState.t;
                    renderState = newRenderState;
                }
            }
            if (stackPos == 0) {
                break;
            }
            else {
                node = stack[--stackPos];
                continue;
            }
        }

        else {
            Node left_child = unpackNode(node.contents);
            Node right_child = unpackNode(node.contents + 1);

            float dist1 = hit(ray, left_child, nearestHit);
            float dist2 = hit(ray, right_child, nearestHit);

            if (dist1 > dist2) {
                Node temp = left_child;
                left_child = right_child;
                right_child = temp;

                float temp_dist = dist1;
                dist1 = dist2;
                dist2 = temp_dist;
            }

            if (dist1 > nearestHit) {
                if (stackPos == 0) {
                    break;
                }
                else {
                    stackPos -= 1;
                    node = stack[stackPos];
                }
            }
            else {
                node = left_child;
                if (dist2 <= nearestHit) {
                    stack[stackPos] = right_child;
                    stackPos += 1;
                }
            }
        }
    }

    if (!renderState.hit) {
        renderState.color = vec3(texture(sky_cube, ray.direction));
    }
        
    return renderState;
}

RenderState hit(Ray ray, Sphere sphere, float tMin, float tMax, RenderState renderState) {

    vec3 co = ray.origin - sphere.center;
    float a = dot(ray.direction, ray.direction);
    float b = 2 * dot(ray.direction, co);
    float c = dot(co, co) - sphere.radius * sphere.radius;
    float discriminant = b * b - (4 * a * c);
    
    if (discriminant > 0.0) {

        float t = (-b - sqrt(discriminant)) / (2 * a);

        if (t > tMin && t < tMax) {

            renderState.position = ray.origin + t * ray.direction;
            renderState.normal = normalize(renderState.position - sphere.center);
            renderState.t = t;
            renderState.color = sphere.color;
            renderState.roughness = sphere.roughness;
            renderState.hit = true;
            return renderState;
        }
    }
    renderState.hit = false;
    return renderState;
}

float hit(Ray ray, Node node, float nearestHit) {

    vec3 tMin = (node.min_corner - ray.origin) / ray.direction;
    vec3 tMax = (node.max_corner - ray.origin) / ray.direction;
    vec3 t1 = min(tMin, tMax);
    vec3 t2 = max(tMin, tMax);
    float tNear = max(max(t1.x, t1.y), t1.z);
    float tFar = min(min(t2.x, t2.y), t2.z);
    if (tNear <= tFar && tFar > 0 && tNear < nearestHit) {
        return tNear;
    }
    else {
        return 999999999;
    }
}

Sphere unpackSphere(int index) {

    // sphere: (cx cy cz r) (r g b roughness)

    int group = int(index/1024);
    int member = int(mod(index,1024));

    Sphere sphere;
    vec4 attributeChunk = imageLoad(objects, ivec2(2 * group, member));
    sphere.center = attributeChunk.xyz;
    sphere.radius = attributeChunk.w;
    
    attributeChunk = imageLoad(objects, ivec2(2 * group + 1, member));
    sphere.color = attributeChunk.xyz;
    sphere.roughness = attributeChunk.w;

    return sphere;
}

Node unpackNode(int index) {

    // node: (x_min, y_min, z_min, sphere_count) (x_max, y_max, z_max, contents)

    int group = int(index/1024);
    int member = int(mod(index,1024));

    Node node;
    vec4 attributeChunk = imageLoad(nodes, ivec2(2 * group, member));
    node.min_corner = attributeChunk.xyz;
    node.sphere_count = int(attributeChunk.w);
    
    attributeChunk = imageLoad(nodes, ivec2(2 * group + 1, member));
    node.max_corner = attributeChunk.xyz;
    node.contents = int(attributeChunk.w);

    return node;
}

int lookupSphereIndex(int index) {
    // index: (index _ _ _)

    int group = int(index/1024);
    int member = int(mod(index,1024));

    vec4 attributeChunk = imageLoad(sphere_lookup_table, ivec2(group, member));

    return int(attributeChunk.x);
}