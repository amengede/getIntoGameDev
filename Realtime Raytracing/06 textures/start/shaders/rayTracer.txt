#version 430

struct Sphere {
    vec3 center;
    float radius;
    vec3 color;
    float roughness;
};

struct Camera {
    vec3 position;
    vec3 forwards;
    vec3 right;
    vec3 up;
};

struct Ray {
    vec3 origin;
    vec3 direction;
};

struct Plane {
    vec3 center;
    vec3 tangent;
    vec3 bitangent;
    vec3 normal;
    float uMin;
    float uMax;
    float vMin;
    float vMax;
    vec3 color;
    float roughness;
};

struct RenderState {
    float t;
    vec3 color;
    vec3 position;
    vec3 normal;
    bool hit;
    float roughness;
};

// input/output
layout(local_size_x = 1, local_size_y = 1) in;
layout(rgba32f, binding = 0) uniform image2D img_output;

//Scene data
uniform Camera viewer;
layout(rgba32f, binding = 1) readonly uniform image2D objects;
layout(rgba32f, binding = 2) readonly uniform image2D noise;
uniform float sphereCount;
uniform float planeCount;

RenderState trace(Ray ray);

Sphere unpackSphere(int index);

Plane unpackPlane(int index);

RenderState hit(Ray ray, Sphere sphere, float tMin, float tMax, RenderState renderstate);

RenderState hit(Ray ray, Plane plane, float tMin, float tMax, RenderState renderstate);

void main() {

    ivec2 pixel_coords = ivec2(gl_GlobalInvocationID.xy);
    ivec2 screen_size = imageSize(img_output);

    vec3 finalColor = vec3(0.0);
    for (int i = 0; i < 4; i++) {

        vec2 screenDeflection = imageLoad(
            noise, 
            ivec2(
                pixel_coords.x + i * screen_size.x,
                pixel_coords.y
            )
        ).xy;
        
        float horizontalCoefficient = float(pixel_coords.x) + screenDeflection.x;
        horizontalCoefficient = (horizontalCoefficient * 2 - screen_size.x) / screen_size.x;
        
        float verticalCoefficient = float(pixel_coords.y) + screenDeflection.y;
        verticalCoefficient = (verticalCoefficient * 2 - screen_size.y) / screen_size.x;

        Ray ray;
        ray.origin = viewer.position;
        ray.direction = viewer.forwards + horizontalCoefficient * viewer.right + verticalCoefficient * viewer.up;
    
        vec3 pixel = vec3(1.0);

        for (int bounce = 0; bounce < 4; bounce++) {
        

            RenderState renderState = trace(ray);

            //early exit
            if (!renderState.hit) {
                break;
            }
        
            //unpack color
            pixel = pixel * renderState.color;

            //set up ray for next trace
            ray.origin = renderState.position;
            ray.direction = reflect(ray.direction, renderState.normal);
            vec3 variation = imageLoad(
                noise, 
                ivec2(
                    pixel_coords.x + bounce * screen_size.x,
                    pixel_coords.y
                )
            ).xyz;
            ray.direction = normalize(ray.direction + renderState.roughness * variation);

        }

        finalColor += 0.25 * pixel;
    }

    imageStore(img_output, pixel_coords, vec4(finalColor,1.0));
}

RenderState trace(Ray ray) {

    RenderState renderState;
    renderState.hit = false;
    renderState.color = vec3(1.0);
    
    float nearestHit = 999999999;
    
    for (int i = 0; i < sphereCount; i++) {

        RenderState newRenderState = hit(ray, unpackSphere(i), 0.001, nearestHit, renderState);

        if (newRenderState.hit) {
            nearestHit = newRenderState.t;
            renderState = newRenderState;
        }
    }

    for (int i = int(sphereCount); i < planeCount + sphereCount; i++) {
    
       RenderState newRenderState = hit(ray, unpackPlane(i), 0.001, nearestHit, renderState);
    
       if (newRenderState.hit) {
            nearestHit = newRenderState.t;
            renderState = newRenderState;
        }
    }
        
    return renderState;
}

RenderState hit(Ray ray, Sphere sphere, float tMin, float tMax, RenderState renderState) {

    vec3 co = ray.origin - sphere.center;
    float a = dot(ray.direction, ray.direction);
    float b = 2 * dot(ray.direction, co);
    float c = dot(co, co) - sphere.radius * sphere.radius;
    float discriminant = b * b - (4 * a * c);
    
    if (discriminant > 0.0) {

        float t = (-b - sqrt(discriminant)) / (2 * a);

        if (t > tMin && t < tMax) {

            renderState.position = ray.origin + t * ray.direction;
            renderState.normal = normalize(renderState.position - sphere.center);
            renderState.t = t;
            renderState.color = sphere.color;
            renderState.roughness = sphere.roughness;
            renderState.hit = true;
            return renderState;
        }
    }
    renderState.hit = false;
    return renderState;
}

RenderState hit(Ray ray, Plane plane, float tMin, float tMax, RenderState renderState) {
    
    float denom = dot(plane.normal, ray.direction); 
    
    if (denom < 0.000001) {

        float t = dot(plane.center - ray.origin, plane.normal) / denom; 

        if (t > tMin && t < tMax) {

            vec3 testPoint = ray.origin + t * ray.direction;
            vec3 testDirection = testPoint - plane.center;

            float u = dot(testDirection, plane.tangent);
            float v = dot(testDirection, plane.bitangent);

            if (u > plane.uMin && u < plane.uMax && v > plane.vMin && v < plane.vMax) {
                renderState.position = testPoint;
                renderState.t = t;
                renderState.color = plane.color;
                renderState.roughness = plane.roughness;
                renderState.normal = plane.normal;
                renderState.hit = true;
                return renderState;
            }
        }
    }
    renderState.hit = false;
    return renderState;
}

Sphere unpackSphere(int index) {

    // sphere: (cx cy cz r) (r g b roughness) (- - - -) (- - - -) (- - - -)

    Sphere sphere;
    vec4 attributeChunk = imageLoad(objects, ivec2(0,index));
    sphere.center = attributeChunk.xyz;
    sphere.radius = attributeChunk.w;
    
    attributeChunk = imageLoad(objects, ivec2(1,index));
    sphere.color = attributeChunk.xyz;
    sphere.roughness = attributeChunk.w;

    return sphere;
}

Plane unpackPlane(int index) {

    // plane: (cx cy cz tx) (ty tz bx by) (bz nx ny nz) (umin umax vmin vmax) (r g b roughness)

    Plane plane;
    vec4 attributeChunk = imageLoad(objects, ivec2(0,index));
    plane.center = attributeChunk.xyz;
    plane.tangent.x = attributeChunk.w;
    
    attributeChunk = imageLoad(objects, ivec2(1,index));
    plane.tangent.yz = attributeChunk.xy;
    plane.bitangent.xy = attributeChunk.zw;

    attributeChunk = imageLoad(objects, ivec2(2,index));
    plane.bitangent.z = attributeChunk.x;
    plane.normal = attributeChunk.yzw;

    attributeChunk = imageLoad(objects, ivec2(3,index));
    plane.uMin = attributeChunk.x;
    plane.uMax = attributeChunk.y;
    plane.vMin = attributeChunk.z;
    plane.vMax = attributeChunk.w;

    attributeChunk = imageLoad(objects, ivec2(4,index));
    plane.color = attributeChunk.xyz;
    plane.roughness = attributeChunk.w;

    return plane;
}